# ~/.zshrc

# -----------------
# Zsh options
# -----------------
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt SHARE_HISTORY
setopt INC_APPEND_HISTORY

bindkey -e
WORDCHARS=${WORDCHARS//[\/]}

# -----------------
# Helpers and OS detection
# -----------------
_has() { (( ${+commands[$1]} )); }

IS_GENTOO=0
if [[ -r /etc/os-release ]]; then
  # shellcheck disable=SC1091
  . /etc/os-release
  [[ ${ID:-} == gentoo ]] && IS_GENTOO=1
fi
[[ -e /etc/gentoo-release ]] && IS_GENTOO=1

# -----------------
# Zim module config
# -----------------
ZSH_AUTOSUGGEST_MANUAL_REBIND=1
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)

# -----------------
# Pager and man
# -----------------
export EDITOR=nvim

if _has bat; then
  export PAGER=bat
  export MANPAGER='bat -l man -p --paging=always'
else
  export PAGER=less
  export MANPAGER='less -R'
fi
export MANROFFOPT='-c'

# -----------------
# Initialize Zim
# -----------------
ZIM_HOME=${ZDOTDIR:-$HOME}/.zim
if [[ ! -e $ZIM_HOME/zimfw.zsh ]]; then
  if _has curl; then
    curl -fsSL --create-dirs -o $ZIM_HOME/zimfw.zsh \
      https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
  else
    mkdir -p $ZIM_HOME && wget -nv -O $ZIM_HOME/zimfw.zsh \
      https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
  fi
fi
if [[ ! $ZIM_HOME/init.zsh -nt ${ZIM_CONFIG_FILE:-${ZDOTDIR:-$HOME}/.zimrc} ]]; then
  source $ZIM_HOME/zimfw.zsh init
fi
source $ZIM_HOME/init.zsh

# -----------------
# Keybinds
# -----------------
zmodload -F zsh/terminfo +p:terminfo
for key ('^[[A' '^P' ${terminfo[kcuu1]}) bindkey $key history-substring-search-up
for key ('^[[B' '^N' ${terminfo[kcud1]}) bindkey $key history-substring-search-down
for key ('k') bindkey -M vicmd $key history-substring-search-up
for key ('j') bindkey -M vicmd $key history-substring-search-down
unset key

# Optional: Atuin search on Ctrl+R if the widget exists
(( ${+widgets[atuin-search]} )) && bindkey '^R' atuin-search

# -----------------
# Completion and fzf-tab
# -----------------
zstyle ':completion:*:descriptions' format '[%d]'
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:git-checkout:*' sort false

if (( IS_GENTOO )); then
  zstyle ':completion::complete:*' use-cache 1
  zstyle ':completion:*' cache-path "$HOME/.cache/zsh"
fi

zstyle ':fzf-tab:*' switch-group ',' '.'
zstyle ':fzf-tab:complete:*:*' fzf-flags --height=100% --preview-window=right:wrap

# Directory preview for cd and zoxide
if _has lsd; then
  zstyle ':fzf-tab:complete:cd:*' fzf-preview 'lsd --color=always -1 -- "$realpath"'
  zstyle ':fzf-tab:complete:__zoxide_z:*' fzf-preview 'lsd --color=always -1 -- "$realpath"'
fi

# systemd unit status preview
_has systemctl && zstyle ':fzf-tab:complete:systemctl-*:*' fzf-preview 'SYSTEMD_COLORS=1 systemctl status "$word"'

# Process preview
zstyle ':completion:*:*:*:*:processes' command "ps -u $USER -o pid,user,comm -w -w"
zstyle ':fzf-tab:complete:(kill|ps):argument-rest' fzf-preview \
  '[[ $group == "[process ID]" ]] && ps --pid="$word" -o cmd --no-headers -w -w'
zstyle ':fzf-tab:complete:(kill|ps):argument-rest' fzf-flags --preview-window=down:3:wrap

# Environment variable preview
zstyle ':fzf-tab:complete:(-command-|-parameter-|-brace-parameter-|export|unset|expand):*' \
  fzf-preview 'echo ${(P)word}'

# Git previews
if _has git; then
  if _has delta; then
    zstyle ':fzf-tab:complete:git-(add|diff|restore):*' fzf-preview 'git diff "$word" | delta'
  else
    zstyle ':fzf-tab:complete:git-(add|diff|restore):*' fzf-preview 'git diff "$word"'
  fi
  zstyle ':fzf-tab:complete:git-log:*' fzf-preview 'git log --color=always "$word"'

  if _has bat; then
    zstyle ':fzf-tab:complete:git-help:*' fzf-preview 'git help "$word" | bat -l man -p --color=always'
  else
    zstyle ':fzf-tab:complete:git-help:*' fzf-preview 'git help "$word" | less -R'
  fi

  zstyle ':fzf-tab:complete:git-show:*' fzf-preview \
    'case "$group" in
       "commit tag") git show --color=always "$word" ;;
       *) git show --color=always "$word" '"$(_has delta && echo '| delta' || true)"' ;;
     esac'

  zstyle ':fzf-tab:complete:git-checkout:*' fzf-preview \
    'case "$group" in
       "modified file") git diff "$word" '"$(_has delta && echo '| delta' || true)"' ;;
       "recent commit object name") git show --color=always "$word" '"$(_has delta && echo '| delta' || true)"' ;;
       *) git log --color=always "$word" ;;
     esac'
fi

# tldr preview
_has tldr && zstyle ':fzf-tab:complete:tldr:argument-1' fzf-preview 'tldr --color always "$word"'

# Command preview
zstyle ':fzf-tab:complete:-command-:*' fzf-preview \
  '(tldr --color always "$word" 2>/dev/null) || (MANWIDTH=$FZF_PREVIEW_COLUMNS man "$word" 2>/dev/null) || command -v "$word" || echo "${(P)word}"'

# File preview
if _has bat; then
  zstyle ':fzf-tab:complete:*:*' fzf-preview \
    'bat --style=plain --color=always --paging=never -- "${(Q)realpath}" 2>/dev/null || less -R -- "${(Q)realpath}" 2>/dev/null || file --brief --mime -- "${(Q)realpath}"'
else
  zstyle ':fzf-tab:complete:*:*' fzf-preview \
    'less -R -- "${(Q)realpath}" 2>/dev/null || file --brief --mime -- "${(Q)realpath}"'
fi

# tmux popup support if available
_has ftb-tmux-popup && zstyle ':fzf-tab:*' fzf-command ftb-tmux-popup
zstyle ':fzf-tab:*' popup-min-size 50 8
zstyle ':fzf-tab:complete:diff:*' popup-min-size 80 12

# -----------------
# Environment and PATH
# -----------------
export PATH="$HOME/.local/bin:$PATH"
export PATH="$HOME/.local/share/mise/shims:$PATH"
export npm_config_prefix="$HOME/.local"
export PATH="$HOME/.luarocks/bin:$PATH"

export GOMODCACHE="$HOME/.cache/go/pkg/mod"
export GOPATH="$HOME/.local/lib/go"
export GOBIN="$HOME/.local/lib/go/bin"
export GO111MODULE=on
export PATH="$GOBIN:$GOPATH/bin:$PATH"

# -----------------
# Aliases
# -----------------
_has lsd && alias ls='lsd' && alias ll='lsd -l' && alias la='lsd -a' && alias tree="lsd --tree"

# -----------------
# Third-party init
# -----------------
_has zoxide && eval "$(zoxide init zsh --cmd cd)"
_has atuin && eval "$(atuin init zsh --disable-up-arrow)"
_has starship && [[ $TERM != linux ]] && eval "$(starship init zsh)"

# -----------------
# Transient prompt
# -----------------
function zle-line-init {
  emulate -L zsh
  [[ $CONTEXT == start ]] || return 0

  while true; do
    zle .recursive-edit
    local -i ret=$?
    [[ $ret == 0 && $KEYS == $'\4' ]] || break
    [[ -o ignore_eof ]] || exit 0
  done

  local saved_prompt=$PROMPT saved_rprompt=$RPROMPT
  PROMPT='$fg[green]‚ùØ '
  RPROMPT=''
  zle .reset-prompt
  PROMPT=$saved_prompt
  RPROMPT=$saved_rprompt

  (( ret )) && zle .send-break || zle .accept-line
  return ret
}
zle -N zle-line-init

